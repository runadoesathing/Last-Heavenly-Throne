EventPointerTable(0xD1, PointerList)

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN UnitsP UnitsP  //This unit group determines the number and position of deployment slots when there is a prep screen
POIN $0 $0 $0
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TURN 0x0 CleartheSeal [1,0] 0x0
TURN 0x0 BandoleoNam [4,0] 0x0
TURN 0x0 Bamboleo [5,0] 0x0
TURN 0x0 SevenSeven [8,0] 0x0
END_MAIN

CharacterBasedEvents:
CharacterEventBothWays(0x8, NaitramyaCRecruit, Cethern, Naitramya)
CharacterEventBothWays(0x8, NaitramyaFRecruit, Ferdiad, Naitramya)
CharacterEventBothWays(0x8, NaitramyaSRecruit, Setanta, Naitramya)
END_MAIN

LocationBasedEvents:
Village(0x9,SteelAxeVillage,7,2)
Village(0xA,SlimBowVillage,2,3)
Village(0xB,PureWaterVillage,7,11)
Village(0xC,ShineGuardVillage,13,9)
House(0x0,AinghaHouse,9,2)
END_MAIN

MiscBasedEvents:
CauseGameOverIfLordDies
END_MAIN

Dunno1:
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN

UnitsP:
UNIT Cethern EirikaLord 0x1 Level(1, Ally, 0)    [0,9]  0x0 0x0 0x1 REDA1R9  [0x0] [0x0,0x0,0x0,0x0]
UNIT Setanta AxeCav 0x1 Level(1, Ally, 0)        [0,8]  0x0 0x0 0x1 REDA0R8  [0x0] [0x0,0x0,0x0,0x0]
UNIT Ferdiad BowCav 0x1 Level(1, Ally, 0)        [0,10] 0x0 0x0 0x1 REDA0R10 [0x0] [0x0,0x0,0x0,0x0]
UNIT

NaitramyaP:
UNIT Naitramya BowKnight 0x1 Level(1, NPC, 0)    [4,8] 0x0 0x0 0x1 REDA4R8 [IronBow, Vulnerary] NeverMoveAI
UNIT

EnemyUnits:
UNIT Sarmaree Soldier 0x0 Level(5, Enemy, 1)         [14,3]  0 0 1 REDA14R3        [Javelin]   NeverMoveAI
UNIT Malava Archer 0x0 Level(1, Enemy, 1)            [11,9]  0 0 1 REDA11R9        [IronBow]   AttackInRangeAI
UNIT Malava LanceKnight 0x0 Level(1, Enemy, 1)       [6,9]   0 0 1 REDA6R9         [IronLance] NeverMoveAI
UNIT MalavaMag Diabolist 0x0 Level(1, Enemy, 1)      [8,4]   0 0 1 REDA8R4         [Fire]      AttackInRangeAI
UNIT Malava Myrmidon 0x0 Level(1, Enemy, 1)          [10,5]  0 0 1 REDA10R5        [IronSword] AttackInRangeAI
UNIT Malava Fighter 0x0 Level(1, Enemy, 1)           [1,4]   0 0 1 REDA1R4         [IronAxe]   [0,6,4,0]
UNIT Malava SwordCav 0x0 Level(2, Enemy, 1)          [11,2]  0 0 1 REDA11R2        [IronSword] AttackInRangeAI
UNIT Malava AxeCav 0x0 Level(1, Enemy, 1)            [11,14] 0 0 1 REDA11R14       [IronAxe]   [0,0,4,0]
UNIT Malava Soldier 0x0 Level(1, Enemy, 1)           [7,13]  0 0 1 REDA7R13        [IronLance] [0,3,4,0]
UNIT Malava Soldier 0x0 Level(2, Enemy, 1)           [3,6]   0 0 1 REDA3R6         [IronLance] [0,6,4,0]
UNIT

Bandito1P:
UNIT 0x80 Brigand 0x0 Level(1, Enemy, 1)             [7,0]   0 0 1 REDA7R0         [IronAxe] [0,4,4,0]
UNIT

Bandito2P:
UNIT 0x80 Brigand 0x0 Level(1, Enemy, 1)             [14,14] 0 0 1 REDA14R14       [IronAxe] [0,4,4,0]
UNIT

Bandito3P:
UNIT 0x80 Brigand 0x0 Level(1, Enemy, 1)             [0,0]   0 0 1 REDA0R0         [IronAxe] [0,4,4,0]
UNIT

ReinforcementsI:
UNIT Anker WyvernLord 0x0 Level(1, Enemy, 1)            [2,14]  0 0 1 REDA2R14        [IronBlade]   [0,0,4,0]
UNIT Alvarin WyvernLord 0x0 Level(1, Enemy, 1)          [3,14]  0 0 1 REDA3R14        [IronPoleax]  [0,0,4,0]
UNIT

Traps1:
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

BeginningScene:
MUSC 0x4C
SetBackground(InsideMapHouse)
BROWNBOXTEXT TheNextDay [8,8] // box in the top right that's pretty cool for small text
MUSC 0x2A
TEXTSTART
TEXTSHOW P1C1MiachAirmedCethern
TEXTEND
REMA
MUSCMID 0x7FFF // fades out the music
FADI 4
STAL 60
MUSC 0x4C
SetBackground(VillageCG)
BROWNBOXTEXT CluainBolg [8,8] // box in the top right that's pretty cool for small text
Text(P1C1xCluainCluain)
MUSCMID 0x7FFF
FADI 4
STAL 60
EnterStop
MUSC 0x5
SetBackground(0x34)
STAL 60
SVAL 0xB 0xFFFFFFFF
STAL 60
TUTORIALTEXTBOXSTART
TEXTSHOW P1C1xWMIntro
TEXTEND
REMA
SVAL 0xB 0xFFFFFFFF
STAL 60
TUTORIALTEXTBOXSTART
TEXTSHOW P1C1xWMIntro2
TEXTEND
REMA
FADI 3
STAL 120
CLEAN
MUSC 0x25
SetBackground(TownCG)
BROWNBOXTEXT CaerRigor [8,8] // box in the top right that's pretty cool for small text
Text(P1C1xCluCluCluClu)
FADI 6
LOAD1 0x1 EnemyUnits
ENUN
CLEAN
LOAD1 0x1 NaitramyaP
ENUN
CAM1 Nairatmya
ENUN
FADU 10
STAL 60
CAM1 [0,11]
ENUN
LOAD2 0x1 UnitsP
ENUN
CUMO Cethern
STAL 60
CURE
Text(P1C1xClClClClClCl)
MUSC 0x9
CALL 0x591F40
SVAL 0xB 0xFFFFFFFF // coordinates for the tutorial text box (0xFFFFFFFF loads in the centre)
STAL 20
TUTORIALTEXTBOXSTART
TEXTSHOW P1C1Warning
TEXTEND
REMA
NoFade
ENDA

EndingScene:
CALL 0x591F40
SVAL 0x3 Clonkeine
GIVEITEMTO Cethern
FADI 4
MNC2 0x1
NoFade
ENDA

NaitramyaFRecruit:
MUSS 0x30
STAL 33
STORETOSOMETHING 0x0
Text(P1C1xFerdiadDagmema)
MURE 0x2
CUSA Naitramya
SVAL 0x1 0x00000000
CHAI Nairatmya
NoFade
ENDA

NaitramyaCRecruit:
MUSS 0x30
STAL 33
STORETOSOMETHING 0x0
Text(P1C1xCethernDagmema)
MURE 0x2
CUSA Naitramya
SVAL 0x1 0x00000000
CHAI Nairatmya
NoFade
ENDA

NaitramyaSRecruit:
MUSS 0x30
STAL 33
STORETOSOMETHING 0x0
Text(P1C1xSetantaDagmema)
MURE 0x2
CUSA Naitramya
SVAL 0x1 0x00000000
CHAI Nairatmya
NoFade
ENDA

BandoleoNam:
CAM1 [7,0]
ENUN
LOAD1 0x1 Bandito1P
ENUN
STAL 60
CAM1 [0,0]
ENUN
LOAD1 0x1 Bandito3P
ENUN
STAL 60
NoFade
ENDA

Bamboleo:
CAM1 [14,14]
ENUN
LOAD1 0x1 Bandito2P
ENUN
STAL 60
NoFade
ENDA

SevenSeven:
CAM1 [2,14]
ENUN
LOAD1 0x1 ReinforcementsI
ENUN
STAL 60
CAM1 Cethern
ENUN
CUMO Cethern
STAL 60
CURE
Text(PlaceholderText)
NoFade
ENDA

SteelAxeVillage:
SetBackground(InsideMapHouse)
TEXTSTART
TEXTSHOW P1C1xSteelAxeVillage
TEXTEND
SOUN 0xAB
TEXTCONT
TEXTEND
REMA
FADI 16
CLEAN
CALL 0x591F40
SVAL 0x3 SteelAxe
GIVEITEMTO 0xFFFF
NoFade
ENDA

SlimBowVillage:
TEXTSTART
SVAL 0x2 InsideMapHouse
SVAL 0x3 P1C1xSlimBowVillage
CALL 0x9EE310
REMA
CALL 0x591F40
SVAL 0x3 SlimBow
GIVEITEMTO 0xFFFF
NoFade
ENDA

PureWaterVillage:
TEXTSTART
SVAL 0x2 InsideMapHouse
SVAL 0x3 P1C1xPureWaterVillage
CALL 0x9EE310
REMA
CALL 0x591F40
SVAL 0x3 PureWater
GIVEITEMTO 0xFFFF
NoFade
ENDA

ShineGuardVillage:
TEXTSTART
SVAL 0x2 InsideMapHouse
SVAL 0x3 P1C1xShineGuardVillage
CALL 0x9EE310
REMA
CALL 0x591F40
SVAL 0x3 ShineGuard
GIVEITEMTO 0xFFFF
NoFade
ENDA

AinghaHouse:
STORETOSOMETHING 0x0 // lowers music volume
MUSI // lowers music volume
SVAL 0x2 InsideMapHouse // decides background 
SVAL 0x3 P1C1xAinghaHouse // decides text id
CALL 0x9EE310 // runs the text
MUNO // increases music volume
NoFade
ENDA

CleartheSeal:
ASMC ClearDebuffs|1
NoFade
ENDA