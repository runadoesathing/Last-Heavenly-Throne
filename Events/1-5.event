EventPointerTable(0x14, PointerList)

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN PrepScreenP PrepScreenP  //This unit group determines the number and position of deployment slots when there is a prep screen
POIN $0 $0 $0
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TURN 0x0 CleartheSeal [1,0] 0x0
TURN 0x0 OutOfTheWoods [2,0] 0x0
TURN 0x0 SummonTheBadGuys [4,0] 0x0
TURN 0x0 SummonTheBadGuys2 [5,0] 0x0
TURN 0x0 GedeonTime [3,0] 0x0
TURN 0x11 SummonTheOchrenHorses [1, 255] 0x0
END_MAIN

CharacterBasedEvents:
CharacterEventBothWays(0xF,GedeonRecruitE,Ethniu,Gedeon)
CharacterEventBothWays(0xF,GedeonRecruitA,Aengus,Gedeon)
END_MAIN

LocationBasedEvents:
Village(0xD,MendVillage,10,2)
Village(0xE,ThunderVillage,4,13)
House(0x0,AinghaHouse,3,4)
House(0x0,RionallHouse,14,0)
House(0x0,LightMagicHouse,10,11)
END_MAIN

MiscBasedEvents:
CauseGameOverIfLordDies
AFEV 0 EndingScene 0x2
/*
AREA 0x8 OutOfTheWoods [0,0] [12,15]
AREA 0x8 OutOfTheWoods [13,15] [13,15]
AREA 0x8 OutOfTheWoods [13,0] [18,9]
AREA 0x8 OutOfTheWoods [13,10] [16,11]
AREA 0x8 OutOfTheWoods [13,12] [14,12]
AREA 0x8 OutOfTheWoods [17,10] [17,10]
AREA 0x8 OutOfTheWoods [19,0] [19,3]
AREA 0x8 OutOfTheWoods [19,6] [19,8]
AREA 0x8 OutOfTheWoods [20,0] [20,0]
*/
AREA 0x10 SummonTheOchrenHorsesArea [0,0] [8,15]
END_MAIN

Dunno1:
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN

PrepScreenP:
UNIT Cethern EirikaLord 0x1 Level(0, Ally, 0)    [20,8] 0x0 0x0 0x0 0x0 [0x0] [0,0,0,0]
UNIT Miach Villager 0x1 Level(0, Ally, 0)        [20,6] 0x0 0x0 0x0 0x0 [0x0] [0,0,0,0]
UNIT Setanta AxeCav 0x1 Level(0, Ally, 0)        [21,9] 0x0 0x0 0x0 0x0 [0x0] [0,0,0,0]
UNIT Setanta AxeCav 0x1 Level(0, Ally, 0)        [20,7] 0x0 0x0 0x0 0x0 [0x0] [0,0,0,0]
UNIT Setanta AxeCav 0x1 Level(0, Ally, 0)        [21,6] 0x0 0x0 0x0 0x0 [0x0] [0,0,0,0]
UNIT Setanta AxeCav 0x1 Level(0, Ally, 0)        [21,7] 0x0 0x0 0x0 0x0 [0x0] [0,0,0,0]
UNIT Setanta AxeCav 0x1 Level(0, Ally, 0)        [21,8] 0x0 0x0 0x0 0x0 [0x0] [0,0,0,0]
UNIT Setanta AxeCav 0x1 Level(0, Ally, 0)        [22,6] 0x0 0x0 0x0 0x0 [0x0] [0,0,0,0]
UNIT Setanta AxeCav 0x1 Level(0, Ally, 0)        [22,7] 0x0 0x0 0x0 0x0 [0x0] [0,0,0,0]
UNIT Setanta AxeCav 0x1 Level(0, Ally, 0)        [22,8] 0x0 0x0 0x0 0x0 [0x0] [0,0,0,0]
UNIT Setanta AxeCav 0x1 Level(0, Ally, 0)        [22,9] 0x0 0x0 0x0 0x0 [0x0] [0,0,0,0]
UNIT

UnitsP:
UNIT Cethern EirikaLord 0x1 Level(1, Ally, 0)    [20,8] 0x0 0x0 0x0 0x0 [0x0] [0x0,0x0,0x0,0x0]
UNIT

DagmemaP:
UNIT Dagmema BowKnight 0x1 Level(1, Ally, 0)    [21,8] 0x0 0x0 0x0 0x0 [0x0] [0x0,0x0,0x0,0x0]
UNIT

SetantaP:
UNIT Setanta AxeCav 0x1 Level(1, Ally, 0)    [20,7] 0x0 0x0 0x0 0x0 [0x0] [0x0,0x0,0x0,0x0]
UNIT

FerdiadP:
UNIT Ferdiad BowCav 0x1 Level(1, Ally, 0)    [21,7] 0x0 0x0 0x0 0x0 [0x0] [0x0,0x0,0x0,0x0]
UNIT

LalithaP:
UNIT Lalitha Mage_F 0x1 Level(1, Ally, 0)    [22,9] 0x0 0x0 0x0 0x0 [0x0] [0x0,0x0,0x0,0x0]
UNIT

BresP:
UNIT Bres Myrmidon 0x1 Level(1, Ally, 0)    [22,8] 0x0 0x0 0x0 0x0 [0x0] [0x0,0x0,0x0,0x0]
UNIT

HanlinP:
UNIT Hanlin Trobairitz 0x1 Level(2, Ally, 0)    [21,6] 0x0 0x0 0x0 0x0 [0x0] [0x0,0x0,0x0,0x0]
UNIT

AengusP:
UNIT Aengus Knight 0x1 Level(1, Ally, 0)    [22,7] 0x0 0x0 0x0 0x0 [0x0] [0x0,0x0,0x0,0x0]
UNIT

EthniuP:
UNIT Ethniu Shaman_F 0x1 Level(1, Ally, 0)    [22,6] 0x0 0x0 0x0 0x0 [0x0] [0x0,0x0,0x0,0x0]
UNIT

SadhbhP:
UNIT Sadhbh Crownflier 0x1 Level(1, Ally, 0)    [22,9] 0x0 0x0 0x1 REDA21R9 [0x0] [0x0,0x0,0x0,0x0]
UNIT

MiachP:
UNIT Miach Villager 0x1 Level(1, Ally, 0)    [21,9] 0x0 0x0 0x1 REDA20R6 [IronSword, Heal, Vulnerary] [0x0,0x0,0x0,0x0]
UNIT

MiachPP:
UNIT Miach Villager 0x1 Level(1, Ally, 0)    [20,6] 0x0 0x0 0x0 0x0 [IronSword, Heal, Vulnerary] [0x0,0x0,0x0,0x0]
UNIT

MiachSoma:
UNIT Soma SwordCav 0x1 Level(0, Ally, 0)    [22,8] 0x0 0x0 0x1 REDA20R6 [0x0] [0x0,0x0,0x0,0x0]
UNIT

EnemyUnits:
UNIT Ishpan Monk 0x0 Level(10, Enemy, 1)              [1,8]   0 0 0 0            [YesodII]    NeverMoveAI
UNIT 0x84 BowCav 0x0 Level(7, Enemy, 1)               [0,7]   DropItem 0 0 0     [SteelBow]   NeverMoveAI
UNIT Ochren Myrmidon_F 0x0 Level(5, Enemy, 1)         [7,15]  DropItem 0 0 0     [SteelSword] NeverMoveAI
UNIT 0x85 Mage 0x0 Level(5, Enemy, 1)                 [11,14] 0 0 0 0            [Wind]       NeverMoveAI 
UNIT 0x85 Shaman 0x0 Level(4, Enemy, 1)               [13,2]  0 0 0 0            [Flux]       NeverMoveAI
UNIT Ochren AxeKnight 0x0 Level(5, Enemy, 1)          [7,3]   0 0 0 0            [SteelAxe]   NeverMoveAI
UNIT 0x84 Fighter Ishpan Level(4, Enemy, 1)           [20,1]  0 0 0 0            [IronAxe]    AttackInRangeAI 
UNIT Ochren Soldier Ishpan Level(5, Enemy, 1)         [18,12] 0 0 0 0            [IronLance]  AttackInRangeAI 
UNIT 0x84 Monk Ishpan Level(4, Enemy, 1)              [15,13] 0 0 0 0            [MalkuthI,Vulnerary]   AttackInRangeAI 
UNIT 0x84 SkyKnight Ishpan Level(5, Enemy, 1)         [7,9]   0 0 0 0            [SlimLance]  NeverMoveAI 
UNIT Ochren SwordKnight Ishpan Level(5, Enemy, 1)     [14,9]  0 0 0 0            [IronBlade]  NeverMoveAI 
UNIT 0x84 AxeCav Ishpan Level(7, Enemy, 1)            [0,9]   0 0 0 0            [SteelAxe]   NeverMoveAI
UNIT 0x85 Diviner_F Ishpan Level(5, Enemy, 1)         [6,12]  0 0 0 0            [MalkuthI]   NeverMoveAI 
UNIT 0x85 Monk Ishpan Level(6, Enemy, 1)              [10,4]  0 0 0 0            [MalkuthI]   NeverMoveAI 
UNIT 0x84 Myrmidon Ishpan Level(5, Enemy, 1)          [3,6]   0 0 0 0            [IronSword]  NeverMoveAI 
UNIT Ochren LanceKnight Ishpan Level(6, Enemy, 1)     [1,12]  0 0 0 0            [Javelin]    NeverMoveAI 
UNIT Ochren SwordCav Ishpan Level(6, Enemy, 1)        [2,14]  0 0 0 0            [SteelSword] NeverMoveAI
UNIT Ochren Archer Ishpan Level(5, Enemy, 1)          [8,2]   0 0 0 0            [IronBow]    NeverMoveAI
UNIT 0x84 Archer_F Ishpan Level(5, Enemy, 1)          [9,12]  0 0 0 0            [BodkinBow]  NeverMoveAI
UNIT Ochren LanceCav Ishpan Level(6, Enemy, 1)        [6,5]   0 0 0 0            [IronLance]  NeverMoveAI 
UNIT 0x84 Soldier Ishpan Level(6, Enemy, 1)           [17,1]  0 0 0 0            [IronLance]  NeverMoveAI 
UNIT OchrenMag Mage_F Ishpan Level(5, Enemy, 1)       [12,0]  0 0 0 0            [Fire,Vulnerary]    NeverMoveAI
UNIT

GedeonP:
UNIT Gedeon Fighter 0x0 Level(4, Enemy, 0)    [3,0] 0 0 1 REDA3R1    [IronAxe, SpectreCard] [0x0,0x6,0x4,0x0]
UNIT 0x84 LanceKnight 0x0 Level(4, Enemy, 1)    [3,0] 0 0 1 REDA3R0    [SteelLance] [0x0,0x6,0x4,0x0]
UNIT 0x85 Mage 0x0 Level(4, Enemy, 1)    [3,0] 0 0 1 REDA4R0    [Fire,Elixir] [0x0,0x6,0x4,0x0]
UNIT

Banditsu:
UNIT 0x81 Pirate 0x0 Level(5, Enemy, 1)    [11,9] 0 0 1 REDA11R9    [IronAxe] [0x0,0x4,0x4,0x0]
UNIT

Banditsu2:
UNIT 0x81 Brigand 0x0 Level(5, Enemy, 1)    [13,0] 0 0 1 REDA12R0    [IronAxe] [0x0,0x4,0x4,0x0]
UNIT

TheOchrens:
UNIT 0x84 SwordCav 0x0 Level(6, Enemy, 1)      [0,6]    0 0 1 REDA0R6     [SteelSword] [0x0,0x0,0x4,0x0]
UNIT 0x84 BowCav Ishpan Level(6, Enemy, 1)     [0,10]   0 0 1 REDA0R10    [SteelBow]   [0x0,0x0,0x4,0x0]
UNIT

CethernMiachMen:
UNIT Cethern Hobelar 0x0 Level(0, Ally, 0)    [3,7]    0 0 1 REDA3R7    [0] [0,0,0,0]
UNIT Miach Villager 0x0 Level(0, Ally, 0)    [3,8]    0 0 1 REDA3R8    [0] [0,0,0,0]
UNIT

TheBoisFromOchren:
UNIT OchrenMag General 0x0 Level(5, Enemy, 1)    [0,8]    0 0 1 REDA1R8    [Spear] [0,0,0,0]
UNIT Ochren SwordCav 0x0 Level(15, Enemy, 1)    [0,7]    0 0 1 REDA1R7    [SteelBlade] [0,0,0,0]
UNIT Ochren LanceCav 0x0 Level(15, Enemy, 1)    [0,6]    0 0 1 REDA0R6    [ShortSpear] [0,0,0,0]
UNIT Ochren AxeCav 0x0 Level(15, Enemy, 1)    [0,9]    0 0 1 REDA1R9   [KillerAxe] [0,0,0,0]
UNIT Ochren LanceCav 0x0 Level(15, Enemy, 1)    [0,10]    0 0 1 REDA0R10    [BraveLance] [0,0,0,0]
UNIT 0x80 SkyKnight 0x0 Level(15, Enemy, 1)    [0,3]    0 0 1 REDA7R5   [SlimLance] [0,0,0,0]
UNIT 0x81 SkyKnight 0x0 Level(15, Enemy, 1)    [0,4]    0 0 1 REDA6R6    [Javelin] [0,0,0,0]
UNIT 0x82 SkyKnight 0x0 Level(15, Enemy, 1)    [0,12]    0 0 1 REDA6R10   [SlimLance] [0,0,0,0]
UNIT 0x83 SkyKnight 0x0 Level(15, Enemy, 1)    [0,13]    0 0 1 REDA7R11    [Javelin] [0,0,0,0]
UNIT

Traps1:
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

BeginningScene:
MUSC 0x48
SetBackground(ArmyCamp)
BROWNBOXTEXT AFewDaysLater [8,8] // box in the top right that's pretty cool for small text
TEXTSTART
CHECK_ALIVE Sadhbh
BEQ 0x0 0x0 0xC
	TEXTSHOW P1C4SadhbhCamp
  TEXTEND
  GOTO 0x1
LABEL 0x0
TEXTSHOW P1C4CethernOnlyCamp
TEXTEND
LABEL 0x1
CHECK_ALIVE Aengus
BEQ 0xD00 0xC 0x0
  CHECK_ALIVE Ethniu
  BEQ 0xC00 0xC 0x0
      TEXTSHOW2 P1C4AECamp
      GOTO 0x2
  LABEL 0xC00
  TEXTSHOW2 P1C4ACamp
  GOTO 0x2
LABEL 0xD00
CHECK_ALIVE Ethniu
BEQ 0x2 0xC 0x0
  TEXTSHOW2 P1C4ECamp
LABEL 0x2
TEXTEND
REMA
FADI 4
STAL 60
MUSC 0x2E
SetBackground(WarriorTent)
TEXTSTART
CHECK_ALIVE Sadhbh
BEQ 0x10 0x0 0xC
	TEXTSHOW P1C4CuRaoiSadhbh
	GOTO 0x11
LABEL 0x10
TEXTSHOW P1C4CuRaoiNoSadhbh
LABEL 0x11
TEXTEND
REMA
FADI 4
STAL 120
MUSC 0x48
SetBackground(HealerTent)
TEXTSTART
TEXTSHOW P1C4MiachTalk
TEXTEND
MUSC 0x27
TEXTCONT
TEXTEND
REMA
FADI 4
STAL 60
MUSC 0x48
SetBackground(CaravanCG)
TEXTSTART
CHECK_ALIVE Sadhbh
BEQ 0x30 0x0 0xC
	TEXTSHOW P1C4MiachEscapeWithSadhbh
	GOTO 0x31
LABEL 0x30
TEXTSHOW P1C4MiachEscape
LABEL 0x31
TEXTEND
REMA
FADI 4
STAL 60
EnterStop
MUSC 0x5
SetBackground(0x34)
STAL 60
SVAL 0xB 0xFFFFFFFF
STAL 60
TUTORIALTEXTBOXSTART
TEXTSHOW P1C4WMIntro
TEXTEND
REMA
SVAL 0xB 0xFFFFFFFF
STAL 60
TUTORIALTEXTBOXSTART
TEXTSHOW P1C4WMIntro2
TEXTEND
REMA
MUSCMID 0x7FFF
FADI 3
STAL 120
CLEAN
EnterStop
MUSC 0x4C
SetBackground(ForestOutskirts)
TEXTSTART
TEXTSHOW P1C4IshpanTalks
TEXTEND
REMA
FADI 4
STAL 60
MUSC 0x24
SetBackground(ForestCG)
CHECK_ALIVE Setanta
BEQ 0x50 0x0 0xC
	CHECK_ALIVE Ferdiad
	BEQ 0x51 0x0 0xC
		TEXTSTART
		TEXTSHOW P1C4SetFerForest
		TEXTEND
		REMA
		GOTO 0x55
	LABEL 0x51
	TEXTSTART
	TEXTSHOW P1C4SetForest
	TEXTEND
	REMA
	GOTO 0x55
LABEL 0x50
CHECK_ALIVE Ferdiad
BEQ 0x52 0x0 0xC
	TEXTSTART
	TEXTSHOW P1C4FerForest
	TEXTEND
	REMA
	GOTO 0x55
LABEL 0x52
TEXTSTART
TEXTSHOW P1C4CethernForest
TEXTEND
REMA
LABEL 0x55
FADI 4
CLEAN
LOAD1 0x1 EnemyUnits
ENUN
LOAD2 0x1 UnitsP
ENUN
AliveCLoad(Setanta,SetantaP,0xBBA)
AliveCLoad(Ferdiad,FerdiadP,0xBBB)
AliveCLoad(Lalitha,LalithaP,0xBBC)
AliveCLoad(Bres,BresP,0xBBD)
AliveCLoad(Ethniu,EthniuP,0xBBE)
AliveCLoad(Aengus,AengusP,0xBBF)
AliveCLoad(Hanlin,HanlinP,0xBB10)
AliveCLoad(Dagmema,DagmemaP,0xBB11)
_0x2620 0x501
MUSC 0x25
FADU 16
STAL 30
BROWNBOXTEXT CaerOchren [8,8] // box in the top right that's pretty cool for small text
_0x2620 0x515
CUMO [20,8]
STAL 60
CURE
TEXTSTART
TEXTSHOW P1C4CethernRealizesTheTruth
TEXTEND
REMA
CHECK_ALIVE Sadhbh
BEQ 0xA0A0 0xC 0x0
	LOAD2 0x1 SadhbhP
	ENUN
	STAL 30
	LOAD1 0x1 MiachP
	ENUN
	REVEAL Sadhbh
	SVAL 0x1 0x1
	SET_STATE Sadhbh
	CUMO [20,7]
	STAL 60
	CURE
	SetBackground(ForestCG)
	TEXTSTART
	TEXTSHOW P1C4SadhbhAndMiachJoin
	TEXTEND
	MUSC 0x30
	TEXTCONT
	TEXTEND
	REMA
	FADI 16
	CLEAN
	GOTO 0xA0AA
LABEL 0xA0A0
LOAD2 0x1 MiachSoma
ENUN
CUMO [20,7]
STAL 60
CURE
SetBackground(ForestCG)
MUSC 0x30
TEXTSTART
TEXTSHOW P1C4MiachJoin
TEXTEND
SOUN 0x2D7
TEXTCONT
TEXTEND
REMA
FADI 16
CLEAN
CLEA 0x0
LOAD2 0x1 UnitsP
ENUN
LABEL 0xA0AA
GotoPrepScreen
NoFade
ENDA

EndingScene:
MUSC 0x26
FADI 6
_0x2620 0x503
ENUT 0x84
CLEA 0x0
CLEE 0x0
CLEN 0x0
LOAD2 0x1 CethernMiachMen
FADU 10
CUMO Cethern
STAL 60
CURE
TEXTSTART
TEXTSHOW P1C4RionallBetray
TEXTEND
REMA
LOAD2 0x1 TheBoisFromOchren
ENUN
CUMO [1,8]
STAL 60
CURE
TEXTSTART
TEXTSHOW P1C4RionallExplains
TEXTEND
REMA
MOVE 0xA Cethern [3,0]
MOVE 0xA Miach [4,0]
ENUN
TEXTSTART
TEXTSHOW P1C4SkyKnightsAway
TEXTEND
REMA
MOVE 0xA 0x80 [1,0]
MOVE 0xA 0x81 [4,0]
MOVE 0xA 0x82 [6,0]
MOVE 0xA 0x83 [8,0]
ENUN
TEXTSTART
TEXTSHOW P1C4AinghaCursesMe
TEXTEND
REMA
MNC2 0x4
FADI 6
ENUF 0x84
NoFade
ENDA

OutOfTheWoods:
//SVAL 0x2 0x0
//CALL 0x9EE1B8
CAM1 Ishpan
CUMO Ishpan
STAL 60
CURE
Text(P1C4IshpanNotices)
SVAL 0x1 0x00040600 // sets ai to [0x0,0x6,0x4,0x0] (chase when approached)
CHAI 0x84 // changes the ai of 0x84
SVAL 0x1 0x00040600 // sets ai to [0x0,0x6,0x4,0x0] (chase when approached)
CHAI 0x85 // changes the ai of 0x85
SVAL 0x1 0x00040300 // sets ai to [0x0,0x3,0x4,0x0] (attack in range)
CHAI Ochren // changes the ai of Ochren
SVAL 0x1 0x00040300 // sets ai to [0x0,0x3,0x4,0x0] (attack in range)
CHAI OchrenMag // changes the ai of OchrenMag
SVAL 0x1 0x20040303 // sets ai to [0x3,0x3,0x4,0x20] (never move)
CHAI [1,12] // changes the ai of this coordinate
SVAL 0x1 0x20040303 // sets ai to [0x3,0x3,0x4,0x20] (never move)
CHAI [7,3] // changes the ai of this coordinate
SVAL 0x1 0x00040300 // sets ai to [0x0,0x3,0x4,0x0] (attack in range)
CHAI [7,9] // changes the ai of this coordinate
SVAL 0x1 0x00040300 // sets ai to [0x0,0x3,0x4,0x0] (attack in range)
CHAI [0,7] // changes the ai of this coordinate
SVAL 0x1 0x00040300 // sets ai to [0x0,0x3,0x4,0x0] (attack in range)
CHAI [0,9] // changes the ai of this coordinate
NoFade
ENDA

GedeonTime:
_0x2620 0x3 //camera better around borders of map
LOAD1 0x1 GedeonP
ENUN
CUMO [3,1]
STAL 60
CURE
TEXTSTART
TEXTSHOW P1C4GedeonAppears
TEXTEND
REMA
NoFade
ENDA

GedeonRecruitA:
MUSC 0x30
TEXTSTART
TEXTSHOW P1C4GedeonAengus
TEXTEND
REMA
CUSA Gedeon
ENUT 0xC
NoFade
ENDA

GedeonRecruitE:
MUSC 0x30
TEXTSTART
TEXTSHOW P1C4GedeonEthniu
TEXTEND
REMA
CUSA Gedeon
ENUT 0xC
NoFade
ENDA

MendVillage:
TEXTSTART
SVAL 0x2 VillageCG
SVAL 0x3 P1C4MendVillage
CALL 0x9EE310
REMA
CALL 0x591F40
SVAL 0x3 Mend
GIVEITEMTO 0xFFFF
NoFade
ENDA

ThunderVillage:
TEXTSTART
SVAL 0x2 VillageCG
SVAL 0x3 P1C4ThunderVillage
CALL 0x9EE310
REMA
CALL 0x591F40
SVAL 0x3 Thunder
GIVEITEMTO 0xFFFF
NoFade
ENDA

AinghaHouse:
STORETOSOMETHING 0x0 // lowers music volume
MUSI // lowers music volume
SVAL 0x2 InsideMapHouse // decides background 
SVAL 0x3 P1C4DiabolistHouse // decides text id
CALL 0x9EE310 // runs the text
MUNO // increases music volume
NoFade
ENDA

LightMagicHouse:
STORETOSOMETHING 0x0 // lowers music volume
MUSI // lowers music volume
SVAL 0x2 InsideMapHouse // decides background 
SVAL 0x3 P1C4LightMagicHouse // decides text id
CALL 0x9EE310 // runs the text
MUNO // increases music volume
NoFade
ENDA

RionallHouse:
STORETOSOMETHING 0x0 // lowers music volume
MUSI // lowers music volume
SVAL 0x2 InsideMapHouse // decides background 
SVAL 0x3 P1C4RionallHouse // decides text id
CALL 0x9EE310 // runs the text
MUNO // increases music volume
NoFade
ENDA

SummonTheBadGuys:
_0x2620 0x90B
LOAD1 0x1 Banditsu
STAL 60
NoFade
ENDA

SummonTheBadGuys2:
_0x2620 0xC
LOAD1 0x1 Banditsu2
STAL 60
NoFade
ENDA

SummonTheOchrenHorses:
CHECK_EVENTID 0x10
BEQ 0x0 0x0 0xC
	_0x2620 0x800
	LOAD1 0x1 TheOchrens
	STAL 60
    GOTO 0x1
LABEL 0x0
    ENUF 0x11
LABEL 0x1
NoFade
ENDA

SummonTheOchrenHorsesArea:
SVAL 0x2 0x0
CALL 0x9EE1B8
NoFade
ENDA

CleartheSeal:
MUSC 0x9
CHECK_ALIVE Hanlin
BEQ 0xB0B0 0x0 0xC
  CAM1 Hanlin
	CUMO Hanlin
	STAL 60
	CURE
	TEXTSTART
	TEXTSHOW P1C4HanlinExplains
	TEXTEND
	REMA
	GOTO 0xB0BB
LABEL 0xB0B0
CAM1 Cethern
CUMO Cethern
STAL 60
CURE
TEXTSTART
TEXTSHOW P1C4CethernExplains
TEXTEND
REMA
LABEL 0xB0BB
SVAL 0xB 0xFFFFFFFF // coordinates for the tutorial text box (0xFFFFFFFF loads in the centre)
STAL 20
TUTORIALTEXTBOXSTART
TEXTSHOW P1C4Warning
TEXTEND
REMA
NoFade
ENDA