EventPointerTable(0x24, PointerList)

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN UnitsP UnitsP  //This unit group determines the number and position of deployment slots when there is a prep screen
POIN $0 $0 $0
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TURN 0x0 Warning [1,0] 0x0
TURN 0x0 DisrespectMe [2,0] 0x0
TURN 0x0 AnotherOne [3,0] 0x0
TURN 0x0 AndAnotherOne [4,0] 0x0
TURN 0x0 YetAnotherOne [5,0] 0x0
TURN 0x0 StillAnotherOne [6,0] 0x0
TURN 0x0 AnotherAnother [7,0] 0x0
TURN 0x0 ChakoraMove [8,0] 0x0
TURN 0x0 FinalOne [9,0] 0x0
TURN 0x0 EndingScene [10,0] 0x0
END_MAIN

CharacterBasedEvents:
CharacterEventBothWays(0xA, TalkEventSM, Sanjana, Marici)
CharacterEventBothWays(0xB, TalkEventVN, Varuna, Nala)
CharacterEventBothWays(0xC, TalkEventNS, Nala, Soma)
END_MAIN

LocationBasedEvents:
Chest(RallyLuckManual, 5, 1)
Chest(IronPike, 5, 3)
Door(7, 2)
END_MAIN

MiscBasedEvents:
CauseGameOverIfLordDies
AREA 0x65 DidWeLose [4,0] [13,4]
END_MAIN

Dunno1:
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN

Traps1:
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

UnitsP:
UNIT Srihwen Sibyl 0x0 Level(0, Ally, 0)    [17,5] 0 0 1 REDA16R5 [0x0] [0,0,0,0]
UNIT Soma Soldier_F 0x0 Level(0, Ally, 0)    [17,6] 0 0 1 REDA17R6 [0x0] [0,0,0,0]
UNIT Ethniu Mystic_F 0x0 Level(0, Ally, 0)    [17,4] 0 0 1 REDA17R4 [0x0] [0,0,0,0]
UNIT Marici BowCav 0x0 Level(0, Ally, 0)    [17,4] 0 0 1 REDA15R4 [0x0] [0,0,0,0]
UNIT

SrughnaGang:
UNIT Nala SwordKnight 0x0 Level(8, NPC, 0)      [11,5] 0 0 0 0 [IronSword,SteelSword,Vulnerary] [0,0,0,0]
UNIT Sanjana LanceCav_F 0x0 Level(2, NPC, 0)    [13,7] 0 0 0 0 [IronLance,Vulnerary] [0,0,0,0]
UNIT Varuna WyvernRider 0x0 Level(2, NPC, 0)    [14,8] 0 0 0 0 [IronAxe,Vulnerary] [0,0,0,0]
UNIT

EnemyGang:
UNIT Chakora Archer_F 0x0 Level(7, Enemy, 1)            [1,3]  DropItem 0 0 0 [SteelBow, Talisman]    [0,3,4,0]
UNIT AntariksaMag Diviner 0x0 Level(2, Enemy, 1)        [5,6]  0 0 0 0        [MalkuthI]              [7,1,4,0]
UNIT Antariksa Woodsman 0x0 Level(2, Enemy, 1)          [3,8]  DropItem 0 0 0 [IronAxe, ChestKey]      [7,1,4,0]
UNIT Antariksa Mercenary 0x0 Level(2, Enemy, 1)         [0,3]  0 0 0 0        [IronSword]             [7,1,4,0]
UNIT Antariksa Soldier 0x0 Level(1, Enemy, 1)           [10,8] DropItem 0 0 0 [IronLance, DoorKey]    [7,1,4,0]
UNIT AntariksaMag Diabolist_F 0x0 Level(2, Enemy, 1)    [3,5]  DropItem 0 0 0 [Fire,Vulnerary]        [7,1,4,0]
UNIT

TurnTwoReinforcements:
UNIT Antariksa Soldier 0x0 Level(2, Enemy, 1)           [0,0] 0 0 1 REDA0R0        [IronLance]            [7,1,4,0]
UNIT Antariksa BowKnight 0x0 Level(2, Enemy, 1)         [2,0] 0 0 1 REDA2R1        [IronBow]             [7,1,4,0]
UNIT

TurnThreeReinforcements:
UNIT Antariksa Woodsman 0x0 Level(3, Enemy, 1)          [1,0] 0 0 1 REDA1R1 [IronAxe]    [7,1,4,0]
UNIT

TurnFourReinforcements:
UNIT Antariksa Myrmidon_F 0x0 Level(1, Enemy, 1)        [0,0] 0 0 1 REDA0R1    [IronSword]   [7,1,4,0]
UNIT

TurnFourReinforcements2:
UNIT AntariksaMag Diviner 0x0 Level(2, Enemy, 1)        [17,8] DropItem 0 1 REDA17R8  [YesodII, ChestKey]     [7,1,4,0]
UNIT

TurnFiveReinforcements:
UNIT AntariksaMag Diviner 0x0 Level(2, Enemy, 1)        [0,0] 0 0 1 REDA2R0    [MalkuthI]    [7,1,4,0]
UNIT Antariksa LanceCav_F 0x0 Level(3, Enemy, 1)        [0,0] 0 0 1 REDA0R2    [IronLance]     [7,1,4,0]
UNIT

TurnFiveReinforcements2:
UNIT Antariksa SwordCav 0x0 Level(3, Enemy, 1)       [17,8] 0 0 1 REDA17R8  [IronSword]     [7,1,4,0]
UNIT

TurnSixReinforcements:
UNIT Antariksa SkyKnight 0x0 Level(2, Enemy, 1)    [0,1] 0 0 1 REDA1R1 [IronLance]    [7,1,4,0]
UNIT

TurnSevenReinforcements:
UNIT AntariksaMag Diviner 0x0 Level(2, Enemy, 1)    [0,0] 0 0 1 REDA1R1 [YesodII]    [7,1,4,0]
UNIT Antariksa Myrmidon 0x0 Level(2, Enemy, 1)    [0,5] 0 0 1 REDA1R5 [IronSword]    [7,1,4,0]
UNIT

TurnSevenReinforcements2:
UNIT AntariksaMag Diabolist 0x0 Level(2, Enemy, 1)    [17,8] DropItem 0 1 REDA17R8 [Wind, Vulnerary]   [7,1,4,0]
UNIT

TurnNineReinforcements:
UNIT Antariksa AxeCav 0x0 Level(3, Enemy, 1)         [17,4] 0 0 1 REDA17R4 [IronAxe] [7,1,4,0]
UNIT Antariksa LanceKnight 0x0 Level(2, Enemy, 1)    [9,9] 0 0 1 REDA9R9 [IronLance] [7,1,4,0]
UNIT

DidWeLose:
CHECK_ACTIVE
SVAL 1 Chakora
BEQ 0x1 0xC 0x1 //branch if chakora is the active unit
SVAL 1 Antariksa
BEQ 0x1 0xC 0x1 //branch if chakora is the active unit
SVAL 1 AntariksaMag
BEQ 0x1 0xC 0x1 //branch if chakora is the active unit
ENUF 0x65
GOTO 0x2
LABEL 0x1
CAM1 Srihwen
CUMO Srihwen
STAL 60
CURE
Text(P2C2WeLostIt)
LABEL 0x2
NoFade
ENDA

BeginningScene:
MUSC 0x4E
SetBackground(MeetingRoom)
BROWNBOXTEXT DharanMonastery [8,8] // box in the top right that's pretty cool for small text
TEXTSTART
TEXTSHOW P2C2MeetingRoomStuff
TEXTEND
CHECK_ALIVE Marici
BEQ 0x0 0xC 0x0
  TEXTSHOW2 P2C2MariciMR
  TEXTEND
  MUSC 0x28
  TEXTCONT
  TEXTEND
  REMA
  GOTO 0x1
LABEL 0x0
TEXTSHOW2 P2C2NoMariciMR
TEXTEND
MUSC 0x28
TEXTCONT
TEXTEND
REMA
LABEL 0x1
MUSCMID 0x7FFF // it fades out the music you dip
FADI 4
STAL 120
EnterStop
MUSC 0x5
SetBackground(0x34)
SVAL 0xB 0xFFFFFFFF
STAL 60
TUTORIALTEXTBOXSTART
TEXTSHOW P2C2WMIntro
TEXTEND
REMA
SVAL 0xB 0xFFFFFFFF
STAL 60
TUTORIALTEXTBOXSTART
TEXTSHOW P2C2WMIntro2
TEXTEND
REMA
MUSCMID 0x7FFF
FADI 3
STAL 60
CLEAN
EnterStop
CAM1 [1,3]
MUSC 0x24
SetBackground(SparseForest)
BROWNBOXTEXT ChandraText [8,8]
CHECK_ALIVE Marici
BEQ 0x10 0xC 0x0
  Text(P2C2ForestTalk)
  GOTO 0x11
LABEL 0x10
Text(P2C2ForestTalkNoMarici)
LABEL 0x11
MUSCMID 0x7FFF
FADI 4
STAL 60
MUSC 0x25
SetBackground(OutsideCastle)
BROWNBOXTEXT AnusuyaGrove [8,8] // box in the top right that's pretty cool for small text
Text(P2C2OutsideGate)
FADI 5
LOAD1 0x1 SrughnaGang
ENUN
LOAD1 0x1 EnemyGang
ENUN
CLEAN
STAL 60
FADU 7
STAL 30
CUMO Chakora
STAL 60
CURE
Text(P2C2ChakoraChakora)
CAM1 [17,5]
ENUN
STAL 30
LOAD2 0x1 UnitsP
ENUN
STAL 20
CUMO Srihwen
STAL 60
CURE
Text(P2C2SrihwenHere)
FADI 6
STAL 15
GotoPrepScreen
NoFade
ENDA

DisrespectMe:
CAM1 [0,0]
ENUN
LOAD1 0x1 TurnTwoReinforcements
ENUN
STAL 60
NoFade
ENDA

AnotherOne:
CAM1 [0,0]
ENUN
LOAD1 0x1 TurnThreeReinforcements
ENUN
STAL 60
CAM1 Srihwen
CUMO Srihwen
STAL 60
CURE
Text(P2C2ReinWarning)
NoFade
ENDA

AndAnotherOne:
CAM1 [0,0]
ENUN
LOAD1 0x1 TurnFourReinforcements
ENUN
STAL 60
CAM1 [17,8]
LOAD1 0x1 TurnFourReinforcements2
ENUN
STAL 60
NoFade
ENDA

YetAnotherOne:
CAM1 [0,0]
ENUN
LOAD1 0x1 TurnFiveReinforcements
ENUN
STAL 60
CAM1 [17,8]
ENUN
LOAD1 0x1 TurnFiveReinforcements2
ENUN
STAL 60
NoFade
ENDA

StillAnotherOne:
CAM1 [0,0]
ENUN
LOAD1 0x1 TurnSixReinforcements
ENUN
STAL 60
NoFade
ENDA

AnotherAnother:
CAM1 [0,0]
ENUN
LOAD1 0x1 TurnSevenReinforcements
ENUN
STAL 60
CAM1 [17,8]
ENUN
LOAD1 0x1 TurnSevenReinforcements2
ENUN
STAL 60
NoFade
ENDA

FinalOne:
CAM1 [17,4]
ENUN
LOAD1 0x1 TurnNineReinforcements
ENUN
STAL 60
NoFade
ENDA

ChakoraMove:
CHECK_ALIVE Chakora
BEQ 0x0 0xC 0x0
  CAM1 [0,0]
  ENUN
  CUMO Chakora
  STAL 60
  CURE
  Text(P2C2ChakoraMad)
  SVAL 0x1 0x00040107 // sets ai seize ai
  CHAI Chakora // changes the ai
LABEL 0x0
NoFade
ENDA

Warning:
MUSC 0x25
CAM1 [17,5]
ENUN
CUMO Sanjana
STAL 60
CURE
CHECK_ALIVE Marici
BEQ 0x0 0xC 0x0
  Text(P2C2NoticeThem)
  GOTO 0x1
LABEL 0x0
Text(P2C2NoMariciThem)
LABEL 0x1
MUSC 0xA
STAL 60
CALL 0x591F40
SVAL 0xB 0xFFFFFFFF // coordinates for the tutorial text box (0xFFFFFFFF loads in the centre)
TUTORIALTEXTBOXSTART
TEXTSHOW P2C2Warning
TEXTEND
REMA
CUSA Sanjana
CUSA Nala
CUSA Varuna
NoFade
ENDA

EndingScene:
ENUF 0x84
MUSC 0x25
FADI 4
SetBackground(OutsideCastle)
CHECK_EVENTID 0x2
BNE 0x0 0xC 0x0
  TEXTSTART
  TEXTSHOW P2C2ChakoraKalkin
  TEXTEND
  SOUN 0x122
  FAWI 10
  FAWU 10
  FAWI 10
  FAWU 10
  SOUN 0x123
  STAL 30
  TEXTCONT
  TEXTEND
  SOUN 0xD6
  TEXTCONT
  GOTO 0x1
LABEL 0x0
TEXTSTART
TEXTSHOW P2C2SoldierKalkin
LABEL 0x1
TEXTEND
REMA
MUSCMID 0x7FFF
FADI 4
STAL 60
CHECK_ALIVE Ethniu
BEQ 0x2 0xC 0x0
  MUSC 0x31
  SetBackground(OutsideGate)
  Text(P2C2SomaWin)
  GOTO 0x3
LABEL 0x2
MUSC 0x32
SetBackground(OutsideGate)
Text(P2C2LostEthniu)
LABEL 0x3
MUSCMID 0x7FFF
FADI 4
STAL 60
CHECK_ALIVE Nala
BEQ 0x10 0xC 0x0
  MUSC 0x4C
  SetBackground(BrownCastleInterior)
  STAL 60
  CHECK_ALIVE Sanjana
  BEQ 0x11 0xC 0x0
    CHECK_ALIVE Varuna
    BEQ 0x12 0xC 0x0
      Text(P2C2NSV)
      GOTO 0x20
    LABEL 0x12
    Text(P2C2NS)
    GOTO 0x20
  LABEL 0x11
  CHECK_ALIVE Varuna
  BEQ 0x13 0xC 0x0
    Text(P2C2NV)
    GOTO 0x20
  LABEL 0x13
  Text(P2C2NO)
  GOTO 0x20
CHECK_ALIVE Varuna
BEQ 0x14 0xC 0x0
  MUSC 0x4C
  SetBackground(BrownCastleInterior)
  STAL 60
  CHECK_ALIVE Sanjana
  BEQ 0x15 0xC 0x0
    Text(P2C2SV)
    GOTO 0x20
  LABEL 0x15
  Text(P2C2VO)
  GOTO 0x20
CHECK_ALIVE Sanjana
BEQ 0x16 0xC 0x0
  MUSC 0x4C
  SetBackground(BrownCastleInterior)
  STAL 60
  Text(P2C2SO)
  MUSCMID 0x7FFF
  FADI 4
  STAL 60
  GOTO 0xFEFE
LABEL 0x16
GOTO 0xFEFE
LABEL 0x20
MUSCMID 0x7FFF
FADI 4
EnterStop
STAL 60
MUSC 0x4A
SetBackground(HouseNightCG)
STAL 30
BROWNBOXTEXT DharaName [8,8]
TEXTSTART
TEXTSHOW P2C2StartTheSoma
TEXTEND
TEXTSHOW2 P2C2SrihwenInterject
TEXTEND
CHECK_ALIVE Varuna
BEQ 0x32 0xC 0x0
  TEXTSHOW2 P2C2VarunaInterject
  TEXTEND
  CHECK_ALIVE Nala
  BEQ 0x33 0xC 0x0
    TEXTSHOW2 P2C2NalaInterject
    TEXTEND
    GOTO 0x33
LABEL 0x32
CHECK_ALIVE Nala
BEQ 0x33 0xC 0x0
  TEXTSHOW2 P2C2NalaInterject2
  TEXTEND
LABEL 0x33
CHECK_ALIVE Varuna
BEQ 0x34 0xC 0x0
  TEXTSHOW2 P2C2SomaInterject
  TEXTEND
  GOTO 0x37
LABEL 0x34
TEXTSHOW2 P2C2SomaInterjectNV
LABEL 0x37
TEXTEND
CHECK_ALIVE Sanjana
BEQ 0x40 0xC 0x0
  TEXTSHOW2 P2C2SanjanaInterjectQ
  GOTO 0x43
LABEL 0x40
CHECK_ALIVE Varuna
BEQ 0x41 0xC 0x0
  TEXTSHOW2 P2C2VarunaInterjectQ
  GOTO 0x43
LABEL 0x41
TEXTSHOW2 P2C2NalaInterjectQ
LABEL 0x43
TEXTEND
CHECK_ALIVE Ethniu
BEQ 0x50 0xC 0x0
  TEXTSHOW2 P2C2EthniuInterjectMen
  GOTO 0x51
LABEL 0x50
TEXTSHOW2 P2C2SomaInterjectMen
LABEL 0x51
TEXTEND
CHECK_ALIVE Varuna
BEQ 0x52 0xC 0x0
  CHECK_ALIVE Sanjana
  BEQ 0x53 0xC 0x0
    TEXTSHOW2 P2C2VarunaInterjectMen2
    TEXTEND
    GOTO 0x52
  LABEL 0x53
  TEXTSHOW2 P2C2VarunaInterjectMen
  TEXTEND
  GOTO 0x52
LABEL 0x52
CHECK_ALIVE Nala
BEQ 0x54 0xC 0x0
  CHECK_ALIVE Varuna
  BEQ 0x55 0xC 0x0
    TEXTSHOW2 P2C2NalaInterjectMen
    TEXTEND
    GOTO 0x54
  LABEL 0x55
  TEXTSHOW2 P2C2NalaInterjectMenNV
  TEXTEND
LABEL 0x54
CHECK_ALIVE Sanjana
BEQ 0x60 0xC 0x0
  TEXTSHOW2 P2C2SrihwenInterjectMen
  TEXTEND
  CHECK_ALIVE Varuna
  BEQ 0x61 0xC 0x0
    TEXTSHOW2 P2C2SanjanaInterjectMen2
    GOTO 0x62
  LABEL 0x61
  TEXTSHOW2 P2C2SanjanaInterjectMen
  LABEL 0x62
  TEXTEND
  CHECK_ALIVE Ethniu
  BEQ 0x63 0xC 0x0
    TEXTSHOW2 P2C2EthniuInterjectWhy
    GOTO 0x70
  LABEL 0x63
  TEXTSHOW2 P2C2SrihwenInterjectWhy
  GOTO 0x70
LABEL 0x60
TEXTSHOW2 P2C2SrihwenInterjectMen2
LABEL 0x70
TEXTEND
REMA
MUSCMID 0x7FFF
FADI 4
STAL 60
LABEL 0xFEFE

/* FEE3 ending
SetBackground(BlackCG)
Text(FEE3Over)
FADI 4
MNTS 0x0
*/

MNC2 0x8
NoFade
ENDA

TalkEventNS:
STORETOSOMETHING 0x0 // lowers music volume
MUSI // lowers music volume
Text(P2C2TalkNS)
MUNO // increases music volume
NoFade
ENDA

TalkEventSM:
STORETOSOMETHING 0x0 // lowers music volume
MUSI // lowers music volume
Text(P2C2TalkSM)
MUNO // increases music volume
NoFade
ENDA

TalkEventVN:
STORETOSOMETHING 0x0 // lowers music volume
MUSI // lowers music volume
Text(P2C2TalkVN)
MUNO // increases music volume
NoFade
ENDA